dnl Process this file with autoconf to produce a configure script.
AC_INIT(multimer.c)
AM_INIT_AUTOMAKE(rtptools, 1.21)

AC_CANONICAL_HOST

ts_cv_darwin="no"
case "${host}" in
*-*-darwin*)
  AC_DEFINE(unix)
  AC_DEFINE(__darwin__)
  ts_cv_darwin="yes";;
esac

AM_CONDITIONAL(DARWIN, test "$ts_cv_darwin" = yes)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

if test "x$GCC" = "xyes"; then
  CFLAGS="$CFLAGS -Wall"
fi

dnl Checks for libraries.

dnl Substitute for AC_CHECK_FUNC; check for function normally, then in
dnl library

AC_DEFUN(CU_CHECK_LIB,
[AC_CHECK_FUNCS($2, , AC_CHECK_LIB($1, $2))])


CU_CHECK_LIB(nsl, gethostbyname)
CU_CHECK_LIB(nsl, yp_match)
CU_CHECK_LIB(socket, socket)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(search.h sys/time.h unistd.h)
AC_CHECK_HEADER([rpcsvc/ypclnt.h],
                [AC_DEFINE([HAVE_YP],[1],[Define to 1 if you have rpcsvc/ypclnt.h])])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_BIGENDIAN
AC_TYPE_UID_T
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname gettimeofday mktime select \
        strdup strerror strstr strtol uname)

AC_OUTPUT(Makefile)
